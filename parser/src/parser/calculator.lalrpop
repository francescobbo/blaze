use crate::parser::tokenizer;
use crate::ast::Expr;
use crate::unit::Unit;

grammar;

extern {
    type Location = usize;
    type Error = tokenizer::Error;

    enum tokenizer::Token {
        number => tokenizer::Token::Number(<String>),
        unit => tokenizer::Token::Unit(<String>),
        percent => tokenizer::Token::Percent(<String>),
        functionCall => tokenizer::Token::FunctionCall(<String>),
        currency => tokenizer::Token::PrefixCurrency(<String>),
        neg => tokenizer::Token::UnaryNeg,
        not => tokenizer::Token::UnaryNot,
        fact => tokenizer::Token::Factorial,
        lparen => tokenizer::Token::LParen,
        rparen => tokenizer::Token::RParen,

        OpAdd => tokenizer::Token::OpAdd,
        OpSub => tokenizer::Token::OpSub,
        OpMul => tokenizer::Token::OpMul,
        OpDiv => tokenizer::Token::OpDiv,
        OpPow => tokenizer::Token::OpPow,
        OpMod => tokenizer::Token::OpMod,
        OpOr => tokenizer::Token::OpOr,
        OpAnd => tokenizer::Token::OpAnd,
        OpXor => tokenizer::Token::OpXor,
        OpShl => tokenizer::Token::OpShl,
        OpShr => tokenizer::Token::OpShr,
        OpRol => tokenizer::Token::OpRol,
        OpRor => tokenizer::Token::OpRor,
        OpOf => tokenizer::Token::OpOf,
    }
}

pub Expr: Box<Expr> = {
    #[precedence(level="0")] // Highest precedence
    <t:Term> => t,
    neg <t:Term> => Box::new(Expr::UnaryNegation(t)),
    not <t:Term> => Box::new(Expr::UnaryNot(t)),
    <t:Term> fact => Box::new(Expr::UnaryFactorial(t)),
    #[precedence(level="1")] #[assoc(side="right")]
    <l:Expr> OpPow <r:Expr> => Box::new(Expr::Power(l, r)),
    #[precedence(level="2")] #[assoc(side="right")]
    <fc:functionCall> lparen rparen => Box::new(Expr::FunctionCall(fc, vec![])),
    <fc:functionCall> <e:Expr> => Box::new(Expr::FunctionCall(fc, vec![*e])),
    <p:percent> OpOf <e:Expr> => Box::new(Expr::Multiply(Box::new(Expr::Percent(p)), e)),
    #[precedence(level="3")] #[assoc(side="left")]
    <l:Expr> OpMul <r:Expr> => Box::new(Expr::Multiply(l, r)),
    <l:Expr> OpDiv <r:Expr> => Box::new(Expr::Divide(l, r)),
    <l:Expr> OpMod <r:Expr> => Box::new(Expr::Modulo(l, r)),
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expr> OpAdd <r:Expr> => Box::new(Expr::Add(l, r)),
    <l:Expr> OpSub <r:Expr> => Box::new(Expr::Subtract(l, r)),
    #[precedence(level="5")] #[assoc(side="left")]
    <l:Expr> OpShl <r:Expr> => Box::new(Expr::ShiftLeft(l, r)),
    <l:Expr> OpShr <r:Expr> => Box::new(Expr::ShiftRight(l, r)),
    <l:Expr> OpRol <r:Expr> => Box::new(Expr::RotateLeft(l, r)),
    <l:Expr> OpRor <r:Expr> => Box::new(Expr::RotateRight(l, r)),
    #[precedence(level="6")] #[assoc(side="left")]
    <l:Expr> OpAnd <r:Expr> => Box::new(Expr::And(l, r)),
    #[precedence(level="7")] #[assoc(side="left")]
    <l:Expr> OpXor <r:Expr> => Box::new(Expr::Xor(l, r)),
    #[precedence(level="8")] #[assoc(side="left")]
    <l:Expr> OpOr <r:Expr> => Box::new(Expr::Or(l, r)),
};

Term: Box<Expr> = {
    <t:Primary> <u:unit> => Box::new(Expr::WithUnit(t, Unit::from_str(&u))),
    <c:currency> <t:Primary> <u:unit?> => {
        if let Some(u) = u {
            Box::new(Expr::WithUnit(t, Unit::from_str(&(c + "*" + &u))))
        } else {
            Box::new(Expr::WithUnit(t, Unit::from_str(&c)))
        }
    },

    Primary
};

Primary: Box<Expr> = {
    <n:number> => Box::new(Expr::Number(n, Unit::empty())),
    <p:percent> => Box::new(Expr::Percent(p)),
    lparen <Expr> rparen
};